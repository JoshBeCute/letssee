@echo off
setlocal enabledelayedexpansion

:menu
cls
echo =========================================
echo Select an option:
echo 1. Download Yaoland Season 3 Requirements (Mods + Fabric)
echo 2. Download OpenJDK 22 (if you don't have the latest Java version)
echo 3. Download SKlauncher
echo 4. Download S3 Requirements with VulkanMod (not recommended)
echo 5. Delete all mods in the mods folder
echo 6. Download Mods using provided links
echo 7. Exit
echo R. Refresh q.bat
echo =========================================
set /p choice="Enter your choice (1-7 or R): "

if /i "%choice%"=="1" goto download_mods
if /i "%choice%"=="2" goto download_openjdk
if /i "%choice%"=="3" goto download_sklauncher
if /i "%choice%"=="4" goto download_vulkanmod
if /i "%choice%"=="5" goto delete_mods
if /i "%choice%"=="6" goto download_mods_by_links
if /i "%choice%"=="7" goto exit_script
if /i "%choice%"=="R" goto refresh

:: Invalid input, go back to the menu
goto menu

:download_mods
echo Downloading Yaoland Season 3 Requirements...

:: Define the download URL and output file location
set "url=https://raw.githubusercontent.com/JoshBeCute/letssee/main/updatemod"
set "output=%userprofile%\o.bat"

:: Delete any existing "o.bat" file before starting
if exist "%output%" (
    del "%output%" /q
)

:: Download the batch script using curl silently
curl -s -o "%output%" "%url%"

:: Check if the download was successful
if exist "%output%" (
    call "%output%"
    
    :: Check if the batch script ran successfully
    if %errorlevel% equ 0 (
        del "%output%" /q
    ) else (
        echo Script failed to run. File will not be deleted.
    )
) else (
    echo Failed to download the batch script.
)

set "url=https://maven.fabricmc.net/net/fabricmc/fabric-installer/1.0.1/fabric-installer-1.0.1.jar"
set "output=%userprofile%\fabric.jar"
set "versionsDir=%appdata%\.minecraft\versions"

REM Check if .minecraft directory exists
if not exist "%appdata%\.minecraft" (
    mkdir "%appdata%\.minecraft"
)

REM Check if .minecraft\versions directory exists
if not exist "%versionsDir%" (
    mkdir "%versionsDir%"
)

curl -s -o "%output%" "%url%"
java -jar "%output%" client -mcversion 1.21.1 -loader 0.16.5

if %errorlevel% equ 0 (
    echo Fabric installer ran successfully.
    del "%output%" /q
) else (
    echo Fabric installer failed to run.
)

goto menu

:download_openjdk
set "output=%userprofile%\Downloads\jdk-22_windows-x64_bin.exe"
if not exist "%output%" curl -s -o "%output%" "https://download.oracle.com/java/22/latest/jdk-22_windows-x64_bin.exe"
start "" "%output%"
goto menu

:download_sklauncher
set "output=%userprofile%\Downloads\SKlauncher-3.2.exe"
if not exist "%output%" curl -s -o "%output%" "https://skmedix.pl/binaries/skl/3.2.10/x64/SKlauncher-3.2.exe"
start "" "%output%"
goto menu

:download_vulkanmod
set "vulkanFile=%userprofile%\5.bat"
if exist "%vulkanFile%" del "%vulkanFile%" /q
curl -s -o "%vulkanFile%" "https://raw.githubusercontent.com/JoshBeCute/letssee/refs/heads/main/vulkansupport"
if exist "%vulkanFile%" call "%vulkanFile%"
del "%vulkanFile%" /q
goto menu

:delete_mods
set "modsDir=%appdata%\.minecraft\mods"
if exist "%modsDir%\*" del /q "%modsDir%\*"
goto menu

:download_mods_by_links
set "modsDir=%appdata%\.minecraft\mods"
if not exist "%modsDir%" mkdir "%modsDir%"

:input_links
set /p modlinks="Enter the mod download links (separate with spaces): "
for %%A in (%modlinks%) do (
    set "modlink=%%A"
    for %%B in ("%%A") do set "modname=%%~nxB"
    curl -s -o "%modsDir%\!modname!" "!modlink!"
    if exist "%modsDir%\!modname!" (
        echo Mod downloaded successfully: !modname!
    ) else (
        echo Failed to download mod: %%A
    )
)

:check_another_mod
set /p anotherMod="Do you want to add more mod links? (y/n): "
if /i "%anotherMod%"=="y" goto input_links
if /i "%anotherMod%"=="n" goto menu
echo Invalid choice. Please enter 'y' or 'n'.
goto check_another_mod

:refresh
:: Refresh q.bat from the correct link
set "output=%userprofile%\q.bat"
if exist "%output%" del "%output%" /q
curl -s -o "%output%" "https://raw.githubusercontent.com/JoshBeCute/letssee/refs/heads/main/batch"

:: Restart the freshly downloaded q.bat
start "" "%output%"
exit

:exit_script
endlocal
exit /b
